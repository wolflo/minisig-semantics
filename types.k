requires "syntax.k"

module MINISIG-TYPES
  imports DOMAINS
  imports MINISIG-SYNTAX

  // --- Internal representations ---

  syntax ViewId ::= "Nonce"
                   | "Thresh"
                   | "DomSep"
                   | "Signers"

  syntax OpView ::= "View" "(" ViewId ")"

  syntax OpExec ::= "Exec" "(" CallType
                           "," Int
                           "," Address
                           "," Data
                           "," LstSig ")"
  syntax Exp ::= OpView
               | OpExec

  rule nonce()            => View ( Nonce   ) [macro]
  rule threshold()        => View ( Thresh  ) [macro]
  rule allSigners()       => View ( Signers ) [macro]
  rule DOMAIN_SEPARATOR() => View ( DomSep  ) [macro]

  rule execute( OP, VAL, DST, DATA, [ SIGS ] )
    => Exec ( OP , VAL , DST , DATA , SIGS )  [macro]

  // --- Signatures ---

  syntax SignedMsg ::= "{" "sig:" Sig
                       "," "nonce:" Int
                       "," "op:" CallType
                       "," "val:" Int
                       "," "dst:" Address
                       "," "data:" Data "}"

  syntax LstSignedMsg ::= List{SignedMsg, ","}

  syntax LstSignedMsg ::= "#toLstSignedMsg" CallType Int Address Data Int LstSig [function]

  rule #toLstSignedMsg _ _ _ _ _ .LstSig => .LstSignedMsg

  rule #toLstSignedMsg OP VAL DST DATA NONCE ( SIG , SIGS )
    => { sig: SIG, nonce: NONCE, op: OP, val: VAL, dst: DST, data: DATA }
       , #toLstSignedMsg OP VAL DST DATA NONCE SIGS

  // --- Errors and Logs ---

  syntax Error ::= "INVALID_VAL"
                 | "INVALID_CALLVAL"
                 | "UNSPECIFIED"

  syntax FnDetails ::= "execute"
                     | "nonce"
                     | "threshold"
                     | "DOMAIN_SEPARATOR"
                     | "allSigners"

  syntax FnLog ::= "{" "fn:" FnDetails "," "callvalue:" Int "," "return:" KItem "}"

  // --- Utils ---

  syntax Int ::= "pow256" // 2 ^ 256
  rule pow256 => 115792089237316195423570985008687907853269984665640564039457584007913129639936 [macro]

endmodule
